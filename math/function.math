import propositional;
import predicate;
import set;
import relation;

###########################
######## FUNCTIONS ########
###########################

"어떤 [$\langle f, A, B\rangle]가 함수이다.

[$f\subseteq A\times B]이고 임의의 [$x\in A]에 대해 [$.(x, y)\in f]를 만족하는 유일한 [$y]가 존재한다는 뜻이다."
$<<\mathop\mathrm{function}>>\left(#1: #2 \to #3\right)$
st is_fun(cls f, $A$ cls a, $B$ cls b) {
	A(
		subseteq(f, cartesian(a, b)),
		Uin(a, (cls x) => {
			A(
				Xin(b, (cls y) => {
					in(pair(x, y), f)
				}),
				Q((cls y) => {
					in(pair(x, y), f)
				})
			)
		})
	)
}

"함수공간(function space)."
$\left\{#1<<\to>>#2\right\}$
cls fun_set($A$ cls a, $B$ cls b) {
	setbuilder((cls f) => is_fun(f, a, b))
}

theorem fun_set_in_i(cls f: @31, $A$ cls a: @11, $B$ cls b: @21) {
	set(a), set(b), is_fun(f, a, b) |-
		setbuilder_i[
			subset_is_set[
				cartesian_is_set[@h1; @h2];
				Ae1[@h3]
			];
			@h3
		][as in(f, fun_set(a, b))]
}

theorem fun_set_subseteq_cartesian($A$ cls a, $B$ cls b) {
	Ui[(cls f) => cp[in(f, fun_set(a, b)) |-
		setbuilder_i[
			set_i[@h1];
			Ae1[setbuilder_e[@h1]]
		][as in(f, power(cartesian(a, b)))]
	]][as subseteq(fun_set(a, b), power(cartesian(a, b)))]
}

theorem fun_subseteq_cartesian(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |- Ae1[@h1]
}

theorem fun_in_e1(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @211, cls y: @212) {
	is_fun(f, a, b), in(pair(x, y), f) |-
		cartesian_in_e1[in_subseteq[@h2; fun_subseteq_cartesian[@h1]]]
}

theorem fun_im_Xq(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |- Ae2[@h1]
}

theorem fun_im_Xq_m(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @21) {
	is_fun(f, a, b), in(x, a) |-
		mp[@h2; Ue(?, x)[fun_im_Xq[@h1]]]
}

theorem fun_im_X(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |-
		Ui[(cls x) => {
			cp[(
				in(x, a) |-
					Ae1[mp[@h2; Ue(?, x)[fun_im_Xq[@h1]]]]
			)]
		}]
		[as Uin(a, (cls x) => {Xin(b, (cls y) => {in(pair(x, y), f)})})]
}

theorem fun_im_X_m(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @21) {
	is_fun(f, a, b), in(x, a) |-
		mp[@h2; Ue(?, x)[fun_im_X[@h1]]]
}

theorem fun_im_Q(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |-
		Ui[(cls x) => {
			cp[(
				in(x, a) |-
					Ae2[mp[@h2; Ue(?, x)[fun_im_Xq[@h1]]]]
			)]
		}]
		[as Uin(a, (cls x) => {Q((cls y) => {in(pair(x, y), f)})})]
}

theorem fun_im_Q_m(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @21, cls y1: @312, cls y2: @412) {
	is_fun(f, a, b), in(x, a), in(pair(x, y1), f), in(pair(x, y2), f) |-
		mp[Ai[@h3; @h4]; Ue2(?, y1, y2)[mp[@h2; Ue(?, x)[fun_im_Q[@h1]]]]]
}

theorem fun_is_rel(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |- rel_subset_is_rel[
		fun_subseteq_cartesian(f, a, b)[@h1];
		cartesian_is_rel(a, b)
	]
}

theorem fun_composite_is_fun(cls f: @11, cls g: @21, $A$ cls a: @12, $B$ cls b: @13, $C$ cls c: @23) {
	is_fun(f, a, b), is_fun(g, b, c) |-
		with (cls h = rel_composite(g, f)) {
			$h_is_rel = setbuilder_pair_is_rel[][as is_rel(h)];
			
			$c1 = rel_U(is_in(cartesian(a, c)), ?)[
				$h_is_rel;
				Ui2[(cls x, cls z) => cp[in(pair(x, z), h) |-
					$X = setbuilder_pair_e[@h3]
					[as X((cls y) => A(in(pair(x, y), f), in(pair(y, z), g)))];

					$U = Ui[(cls y) => cp2[in(pair(x, y), f), in(pair(y, z), g) |-
						$x_in_a = cartesian_in_e1[in_subseteq[@h4; Ae1[@h1]]];
						$z_in_c = cartesian_in_e2[in_subseteq[@h5; Ae1[@h2]]];

						cartesian_in_i[$x_in_a; $z_in_c]
					]];

					mpX_Xe_p(?, in(pair(x, z), cartesian(a, c)))[$X; $U]
				]]
			][as subseteq(rel_composite(g, f), cartesian(a, c))];
			
			$c23 = Ui[(cls x) => cp[in(x, a) |-
				$fXq = mp[@h3; Ue(?, x)[Ae2[@h1]]];
				$fX = Ae1[$fXq];
				$fQ = Ae2[$fXq];
				
				$c2 = mpX_Xe_p(?, Xin(c, (cls z) => in(pair(x, z), rel_composite(g, f))))[
					$fX;
					Ui[(cls y) => cp2[in(y, b), in(pair(x, y), f) |-
						$gX = Ae1[mp[@h4; Ue(?, y)[Ae2[@h2]]]]
						[as Xin(c, (cls z) => in(pair(y, z), g))];

						mpXin(?, ?, (cls z) => in(pair(x, z), rel_composite(g, f)))[
							$gX;
							Ui[(cls z) => cp[in(z, c) |-
								cp[in(pair(y, z), g) |-
									rel_composite_in_i[@h5; @h7]
								]
							]]
						]
					]]
				];
				
				$c3 = Ui2[(cls z1, cls z2) => cp2[in(pair(x, z1), h), in(pair(x, z2), h) |-
					mpX_Xe_p(?, eq(z1, z2))[
						setbuilder_pair_e[@h4]
						[as X((cls y1) => A(in(pair(x, y1), f), in(pair(y1, z1), g)))];
						Ui[(cls y1) => cp2[in(pair(x, y1), f), in(pair(y1, z1), g) |-
							mpX_Xe_p(?, eq(z1, z2))[
								setbuilder_pair_e[@h5]
								[as X((cls y2) => A(in(pair(x, y2), f), in(pair(y2, z2), g)))];
								Ui[(cls y2) => cp2[in(pair(x, y2), f), in(pair(y2, z2), g) |-
									fun_im_Q_m[
										@h2;
										fun_in_e1[@h2; @h7];
										@h7;
										in_eq_left_2[
											@h9;
											pair_eq_i[
												mp[
													Ai[@h6; @h8];
													Ue2(?, y1, y2)[$fQ]
												];
												eq_reflexive(z2)
											]
										]
									]
								]]
							]
						]]
					]
				]][as Q((cls z) => in(pair(x, z), h))];
				
				Ai[$c2; $c3]
			]];
			
			Ai[$c1; $c23]
			[as is_fun(h, a, c)]
		}
}

theorem fun_dom(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |-
		$E = Ui[(cls x) => {
			$c1 = cp[in(x, rel_dom(f)) |-
				mpX_Xe_p(?, in(x, a))[
					setbuilder_e[@h2];
					Ui[(cls y) => cp[in(pair(x, y), f) |-
						fun_in_e1[@h1; @h3]
					]]
				]
			];

			$c2 = cp[in(x, a) |-
				setbuilder_i[
					set_i[@h2];
					Xin_to_X[fun_im_X_m[@h1; @h2]]
				][as in(x, rel_dom(f))]
			];

			Ei[$c1; $c2]
		}];
		
		eq_i(rel_dom(f), a)[$E]
}

"함수 호출. [$f]가 함수이고 [$x]가 [$f]의 정의역에 있을 경우 함숫값이 된다. 그러하지 아니할 때의 동작은 중요하지 않다."
$#1\mathord{\left(#2\right)}{}^{<<\scriptscriptstyle\dag>>}$
sealed cls funcall(cls f, cls x) {
	instantiate((cls y) => in(pair(x, y), f))
}

theorem funcall_Xq(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @21) {
	is_fun(f, a, b), in(x, a) |-
		$foo = fun_im_Xq_m[@h1; @h2];
		
		Ai[Xin_to_X[Ae1[$foo]]; Ae2[$foo]]
		[as Xq((cls y) => in(pair(x, y), f))]
}

theorem funcall_def(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @21) using funcall {
	is_fun(f, a, b), in(x, a) |-
		instantiate_def[funcall_Xq[@h1; @h2]]
		[as in(pair(x, funcall(f, x)), f)]
}

theorem funcall_i(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @211, cls y: @212) {
	is_fun(f, a, b), in(pair(x, y), f) |-
		$x_in_a = cartesian_in_e1[in_subseteq[@h2; fun_subseteq_cartesian[@h1]]];
		
		fun_im_Q_m[
			@h1;
			$x_in_a;
			@h2;
			funcall_def[@h1; $x_in_a]
		]
}

theorem funcall_e(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @21, cls y: @31) {
	is_fun(f, a, b), in(x, a), eq(y, funcall(f, x)) |-
		in_eq_left_2[
			funcall_def[@h1; @h2];
			pair_eq_i[
				eq_reflexive(x);
				@h3
			]
		]
}

theorem fun_eq_i(cls f: @11, cls g: @21, $A$ cls a: @12, $B$ cls b: @13) {
	$bar = (cls f, cls g, $A$ cls a, $B$ cls b) => {
		is_fun(f, a, b), is_fun(g, a, b), Uin(a, (cls x) => {eq(funcall(f, x), funcall(g, x))}) |-
			$foo = Ui2[(cls x, cls y) => {
				cp[in(pair(x, y), f) |-
					$x_in_a = fun_in_e1[@h1; @h4];
					funcall_e[@h2; $x_in_a; eq_transitive[funcall_i[@h1; @h4]; mp[$x_in_a; Ue(?, x)[@h3]]]]]
			}];

			rel_U(is_in(g), ?)[fun_is_rel[@h1]; $foo]
			[as subseteq(f, g)]
	};
	
	is_fun(f, a, b), is_fun(g, a, b), Uin(a, (cls x) => {eq(funcall(f, x), funcall(g, x))}) |-
		subseteq_to_eq[
			$bar(f, g, a, b)[@h1; @h2; @h3];
			$bar(g, f, a, b)[@h2; @h1; Ui[(cls x) => { cp[in(x, a) |- eq_symmetric[mp[@h4; Ue(?, x)[@h3]]]] }]]
		]
}

theorem funcall_eq_i(cls f, cls x1: @11, cls x2: @12) {
	eq(x1, x2) |-
		call_eq_i((cls x) => funcall(f, x), ?, ?)[@h1]
}

theorem funcall_in_codomain(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x: @21) {
	is_fun(f, a, b), in(x, a) |-
		mpX_Xe_p(?, in(funcall(f, x), b))[
			fun_im_X_m[@h1; @h2];
			Ui[(cls y) => { cp[A(in(y, b), in(pair(x, y), f)) |-
				in_eq_left_1[Ae1[@h3]; fun_im_Q_m[@h1; @h2; Ae2[@h3]; funcall_def(f, a, b, x)[@h1; @h2]]]] }]
		]
}

theorem fun_im_in_codomain(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |-
		Ui[(cls y) => cp[in(y, rel_im(f)) |-
			$Xx_xy_in_f = setbuilder_e[@h2];
			
			mpX_Xe_p(?, in(y, b))[
				$Xx_xy_in_f;
				Ui[(cls x) => cp[in(pair(x, y), f) |-
					cartesian_in_e2[in_subseteq[@h3; Ae1[@h1]]]
				]]
			]
		]][as subseteq(rel_im(f), b)]
}

"매개변수가 하나인 메타함수로 함수를 만든다."
$\left[#1\right]_{<<\in>>#2}$
cls make_fun(fun f, $A$ cls a) {
	setbuilder2(
		(cls z) => {
			pair(z, f(z))
		},
		(cls z) => {
			in(z, a)
		}
	)
}

theorem make_fun_in_i(fun f: @r21, $A$ cls a: @32, cls z: @11) {
	set(z), set(f(z)), in(z, a) |-
		setbuilder2_i(?, ?, z)[
			pair_set_i[@h1; @h2]; @h3
		][as in(pair(z, f(z)), make_fun(f, a))]
}

theorem make_fun_is_fun(fun f: @r11, $A$ cls a: @r2, $B$ cls b: @r3) {
	Uin(a, (cls x) => {
		in(f(x), b)
	}) |-
		$c1 = Ui[(cls w) => { cp[in(w, make_fun(f, a)) |-
			mpX_Xe_p(?, in(w, cartesian(a, b)))[
				setbuilder_e[@h2];
				Ui[(cls z) => { cp2[eq(w, pair(z, f(z))), in(z, a) |-
					in_eq_left_2[cartesian_in_i[@h4; mp[@h4; Ue(?, z)[@h1]]]; @h3]] }]
			]
		] }][as subseteq(make_fun(f, a), cartesian(a, b))];
		
		$c23 = Ui[(cls x) => { cp[in(x, a) |-
			$fx_in_b = mp[@h2; Ue(?, x)[@h1]];
			$c2 = Xi((cls y) => {
				A(in(y, b), in(pair(x, y), make_fun(f, a)))
			}, f(x))[
				Ai[
					$fx_in_b;
					setbuilder2_i(?, ?, x)[pair_set_i[set_i[@h2]; set_i[$fx_in_b]]; @h2]
					[as in(pair(x, f(x)), make_fun(f, a))]
				]
			];
			
			$c3 = Qim((cls y) => {in(pair(x, y), make_fun(f, a))}, f(x))[
				Ui[(cls y) => { cp[in(pair(x, y), make_fun(f, a)) |-
					mpX_Xe_p(?, eq(y, f(x)))[
						setbuilder_e[@h3];
						Ui[(cls z) => { cp[A(eq(pair(x, y), pair(z, f(z))), in(z, a)) |-
							$pair_eq = Ae1[@h4];

							ax_indiscernible_m2((cls z) => {eq(y, f(z))}, ?, ?)[
								pair_eq_e1_v1[set_i[@h3]; $pair_eq];
								pair_eq_e2_v1[set_i[@h3]; $pair_eq]
							]
						] }]
					]
				] }]
			];
			
			Ai[$c2; $c3]
		] }];
		
		Ai[$c1; $c23][as is_fun(make_fun(f, a), a, b)]
}

theorem make_fun_funcall(fun f: @r111, $A$ cls a: @22, $B$ cls b, cls x: @21) {
	Uin(a, (cls x) => {in(f(x), b)}), in(x, a) |-
		eq_symmetric[funcall_i[
			make_fun_is_fun(f, a, b)[@h1];
			setbuilder2_i(?, ?, x)[
				pair_set_i[set_i[@h2]; set_i[mp[@h2; Ue(?, x)[@h1]]]];
				@h2
			][as in(pair(x, f(x)), make_fun(f, a))]
		]]
}

"매개변수가 두 개인 메타함수로 함수를 만든다."
$\left[#1\right]_{<<\in>>#2 \times #3}$
cls make_fun_pair([(cls, cls) -> cls] f, $A$ cls a, $B$ cls b) {
	setbuilder2_pair(
		(cls x, cls y) => {
			pair(pair(x, y), f(x, y))
		},
		(cls x, cls y) => {
			A(in(x, a), in(y, b))
		}
	)
}

"집합 [$A] 상에서의 항등함수(identity function)."
$<<\operatorname{id}>>_{#1}$
cls fun_id($A$ cls a) {
	make_fun((cls x) => x, a)
}

theorem fun_id_is_fun($A$ cls a) {
	make_fun_is_fun(
		(cls x) => x,
		a, a
	)[Ui[(cls x) => Ipp(in(x, a))]]
	[as is_fun(fun_id(a), a, a)]
}

theorem fun_id_funcall($A$ cls a: @12, cls x: @11) {
	in(x, a) |-
		make_fun_funcall((cls x) => x, a, a, x)[
			Ui[(cls x) => Ipp(in(x, a))];
			@h1
		][as eq(funcall(fun_id(a), x), x)]
}

theorem fun_id_im_set($A$ cls a: @12, $B$ cls b: @11) {
	subseteq(b, a) |-
		with (cls f = fun_id(a)) {
			$E = Ui[(cls y) => {
				$c1 = cp[in(y, rel_im_set(f, b)) |-
					mpXin_Xe_p(?, ?, in(y, b))[
						setbuilder_e[@h2];
						Ui[(cls x) => cp[in(x, b) |-
							cp[in(pair(x, y), fun_id(a)) |-
								mpX_Xe_p(?, in(y, b))[
									setbuilder_e[@h4];
									Ui[(cls z) => cp2[eq(pair(x, y), pair(z, z)), in(z, a) |-
										$x_is_z = pair_eq_e1_v1[
											set_i[@h4];
											@h5
										];

										$y_is_z = pair_eq_e2_v1[
											set_i[@h4];
											@h5
										];

										in_eq_left_1[
											@h3;
											eq_transitive[$x_is_z; eq_symmetric[$y_is_z]]
										]
									]]
								]
							]
						]]
					]
				];

				$c2 = cp[in(y, b) |-
					$y_in_a = in_subseteq[@h2; @h1];
					$set_y = set_i[@h2];

					$yy_in_id = make_fun_in_i((cls x) => x, a, y)[$set_y; $set_y; $y_in_a]
					[as in(pair(y, y), f)];

					setbuilder_i[
						$set_y;
						Xi((cls x) => A(in(x, b), in(pair(x, y), f)), y)[Ai[@h2; $yy_in_id]]
					][as in(y, rel_im_set(f, b))]
				];

				Ei[$c1; $c2]
			}];
			
			eq_i(rel_im_set(f, b), b)[$E]
		}
}

"전사함수(surjective function). onto라고도 한다."
$<<\mathop\mathrm{surjection}>>\left(#1: #2 \to #3\right)$
st is_surjection(cls f, $A$ cls a, $B$ cls b) {
	A(
		is_fun(f, a, b),
		Uin(b, (cls y) => {
			Xin(a, (cls x) => {
				in(pair(x, y), f)
			})
		})
	)
}

theorem surjection_i(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_fun(f, a, b) |-
		with ($B'$ cls b2 = rel_im(f)) {
			$c1 = rel_U(is_in(cartesian(a, rel_im(f))), ?)[
				fun_is_rel[@h1];
				Ui2[(cls x, cls y) => cp[in(pair(x, y), f) |-
					$xy_in_ab = in_subseteq[@h2; Ae1[@h1]];
					$x_in_a = cartesian_in_e1[$xy_in_ab];
					$y_in_b = cartesian_in_e2[$xy_in_ab];

					$y_in_im = setbuilder_i[
						set_i[$y_in_b];
						Xi((cls x) => in(pair(x, y), f), x)[@h2]
					][as in(y, rel_im(f))];

					cartesian_in_i[$x_in_a; $y_in_im]
				]]
			][as subseteq(f, cartesian(a, rel_im(f)))];
			
			$c23 = Ui[(cls x) => cp[in(x, a) |-
				$c2 = mpX(?, (cls y) => A(
					in(y, rel_im(f)),
					in(pair(x, y), f)
				))[
					Ae1[mp[@h2; Ue(?, x)[Ae2[@h1]]]];
					Ui[(cls y) => cp2[in(y, b), in(pair(x, y), f) |-
						Ai[cartesian_in_e2[in_subseteq[@h4; $c1]]; @h4]
					]]
				][as Xin(rel_im(f), (cls y) => in(pair(x, y), f))];
				
				$c3 = Ae2[mp[@h2; Ue(?, x)[Ae2[@h1]]]];
				
				Ai[$c2; $c3]
			]];
			
			$is_fun = Ai[$c1; $c23]
			[as is_fun(f, a, rel_im(f))];
			
			$surjective = Ui[(cls y) => cp[in(y, rel_im(f)) |-
				mpX(?, (cls x) => A(
					in(x, a),
					in(pair(x, y), f)
				))[
					setbuilder_e[@h2];
					Ui[(cls x) => cp[in(pair(x, y), f) |-
						Ai[
							cartesian_in_e1[in_subseteq[@h3; Ae1[@h1]]];
							@h3
						]
					]]
				][as Xin(a, (cls x) => in(pair(x, y), f))]
			]];
			
			Ai[$is_fun; $surjective]
			[as is_surjection(f, a, rel_im(f))]
		}
}

"단사함수(injective function). 일대일함수(one-to-one function)라고도 한다."
$<<\mathop\mathrm{injection}>>\left(#1: #2 \to #3\right)$
st is_injection(cls f, $A$ cls a, $B$ cls b) {
	A(
		is_fun(f, a, b),
		Uin(b, (cls y) => {
			Q((cls x) => {
				in(pair(x, y), f)
			})
		})
	)
}

schema injection_dom_Q_m(cls f: @11, $A$ cls a: @12, $B$ cls b: @13, cls x1: @311, cls x2: @411, cls y: @21) {
	is_injection(f, a, b), in(y, b), in(pair(x1, y), f), in(pair(x2, y), f) |-
		mp[
			Ai[@h3; @h4];
			Ue2(?, x1, x2)[mp[@h2; Ue(?, y)[Ae2[@h1]]]]
		]
}

"전단사함수(bijective function). [~(href=#def-is_fun)is_fun]의 정의와의 유사성에 주목하라."
$<<\mathop\mathrm{bijection}>>\left(#1: #2 \to #3\right)$
st is_bijection(cls f, $A$ cls a, $B$ cls b) {
	A(
		is_fun(f, a, b),
		Uin(b, (cls y) => {
			A(
				Xin(a, (cls x) => {
					in(pair(x, y), f)
				}),
				Q((cls x) => {
					in(pair(x, y), f)
				})
			)
		})
	)
}

theorem bijection_i(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_surjection(f, a, b), is_injection(f, a, b) |-
		$is_fun = Ae1[@h1];
		
		Ai[
			$is_fun;
			Ui[(cls y) => cp[in(y, b) |-
				Ai[
					mp[@h3; Ue(?, y)[Ae2[@h1]]];
					mp[@h3; Ue(?, y)[Ae2[@h2]]]
				]
			]]
		][as is_bijection(f, a, b)]
}

theorem bijection_is_surjection(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_bijection(f, a, b) |-
		$is_fun = Ae1[@h1];
		
		Ai[
			$is_fun;
			Ui[(cls y) => cp[in(y, b) |-
				Ae1[mp[@h2; Ue(?, y)[Ae2[@h1]]]]
			]]
		][as is_surjection(f, a, b)]
}

theorem bijection_is_injection(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_bijection(f, a, b) |-
		$is_fun = Ae1[@h1];
		
		Ai[
			$is_fun;
			Ui[(cls y) => cp[in(y, b) |-
				Ae2[mp[@h2; Ue(?, y)[Ae2[@h1]]]]
			]]
		][as is_injection(f, a, b)]
}

theorem injection_to_bijection(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_injection(f, a, b) |-
		$f_is_fun = Ae1[@h1];
		$surjection = surjection_i[$f_is_fun];
		$fun = Ae1[$surjection];
		$f_is_inj = Ae2[@h1];
		
		$inj = Uin_subset[fun_im_in_codomain[$f_is_fun]; $f_is_inj];
		
		$injection = Ai[$fun; $inj]
		[as is_injection(f, a, rel_im(f))];
		
		bijection_i[$surjection; $injection]
}

$<<\mathop\mathrm{invertible\ fun.}>>\left(#1: #2 \to #3\right)$
st is_invertible_fun(cls f, $A$ cls a, $B$ cls b) {
	A(
		is_fun(f, a, b),
		is_fun(rel_inverse(f), b, a)
	)
}

theorem bijection_inverse_is_bijection(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_bijection(f, a, b) |-
		$f_is_fun = Ae1[@h1];
		$f_is_rel = fun_is_rel[$f_is_fun];
		$f_in_ab = Ae1[$f_is_fun];
		$fXq1 = Ae2[$f_is_fun];
		$fXq2 = Ae2[@h1];
		
		$f_inv_in_ba = subseteq_eq[
			rel_inverse_subseteq[$f_is_rel; $f_in_ab];
			cartesian_inverse(a, b)
		];
		
		$f_inv_Xq1 = Ui[(cls y) => cp[in(y, b) |-
			$Xq = mp[@h2; Ue(?, y)[$fXq2]];
			$X = Ae1[$Xq];
			$Q = Ae2[$Xq];
			
			$X_inv = mpXin(?, ?, (cls x) => in(pair(y, x), rel_inverse(f)))[
				$X;
				Ui[(cls x) => cp[in(x, a) |-
					cp[in(pair(x, y), f) |-
						rel_inverse_in_i[@h4]
					]
				]]
			];
			
			$Q_inv = QU[
				$Q;
				Ui[(cls x) => cp[in(pair(y, x), rel_inverse(f)) |-
					rel_inverse_in_e[@h3]
				]]
			][as Q((cls x) => in(pair(y, x), rel_inverse(f)))];
			
			Ai[$X_inv; $Q_inv]
		]];
		
		$f_inv_is_fun = Ai[$f_inv_in_ba; $f_inv_Xq1]
		[as is_fun(rel_inverse(f), b, a)];
		
		$f_inv_Xq2 = Ui[(cls x) => cp[in(x, a) |-
			$Xq = mp[@h2; Ue(?, x)[$fXq1]];
			$X = Ae1[$Xq];
			$Q = Ae2[$Xq];
			
			$X_inv = mpXin(?, ?, (cls y) => in(pair(y, x), rel_inverse(f)))[
				$X;
				Ui[(cls y) => cp[in(y, b) |-
					cp[in(pair(x, y), f) |-
						rel_inverse_in_i[@h4]
					]
				]]
			];
			
			$Q_inv = QU[
				$Q;
				Ui[(cls y) => cp[in(pair(y, x), rel_inverse(f)) |-
					rel_inverse_in_e[@h3]
				]]
			][as Q((cls y) => in(pair(y, x), rel_inverse(f)))];
			
			Ai[$X_inv; $Q_inv]
		]];
		
		Ai[$f_inv_is_fun; $f_inv_Xq2]
		[as is_bijection(rel_inverse(f), b, a)]
}

theorem bijection_is_invertible(cls f: @11, $A$ cls a: @12, $B$ cls b: @13) {
	is_bijection(f, a, b) |-
		Ai[
			Ae1[@h1];
			Ae1[bijection_inverse_is_bijection[@h1]]
		][as is_invertible_fun(f, a, b)]
}

"칸토어의 정리(Cantor's theorem). [~(href=https://www.cs.ru.nl/~freek/100/)[*Formalizing 100 Theorems]]의 정리 #63이다."
theorem cantor($A$ cls a: @11, cls f: @r11) {
	set(a) |-
		Ni_alt[is_surjection(f, a, power(a)) |-
			with ($B$ cls b = subsetbuilder(a, (cls x) => Nin(x, funcall(f, x)))) {
				$f_is_fun = Ae1[@h2];
				
				$b_subseteq_a = id(subseteq(b, a))[
					subsetbuilder_is_subset(a, (cls x) => Nin(x, funcall(f, x)))
				];
				
				$b_in_power_a = power_in_i[@h1; $b_subseteq_a];
				
				$X = mp[$b_in_power_a; Ue(?, b)[Ae2[@h2]]];
				
				$U = Ui[($\xi$ cls xi) => cp2[in(xi, a), in(pair(xi, b), f) |-
					$b_is_f_xi = funcall_i[$f_is_fun; @h4];
					
					$xi_Nin_f_xi = Ni_alt[in(xi, funcall(f, xi)) |-
						Fi[
							@h5;
							subsetbuilder_e[in_eq_right_2[@h5; $b_is_f_xi]]
						]
					][as Nin(xi, funcall(f, xi))];
					
					$xi_in_b = subsetbuilder_i[
						@h3;
						$xi_Nin_f_xi
					][as in(xi, b)];
					
					$xi_in_f_xi = in_eq_right_1[$xi_in_b; $b_is_f_xi];
					
					Fi[$xi_in_f_xi; $xi_Nin_f_xi]
				]];
				
				mpX_Xe_p(?, F)[$X; $U]
			}
		]
}