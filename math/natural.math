import propositional;
import predicate;
import set;
import relation;
import function;

"따름 순서수(successor ordinal)."
${#1}^{<<+>>}$
cls successor(cls x) {
	cup(x, singleton(x))
}

theorem successor_eq_i(cls x: @11, cls y: @12) {
	eq(x, y) |-
		cup_eq_i[@h1; singleton_eq_i[@h1]]
		[as eq(successor(x), successor(y))]
}

theorem self_in_successor(cls x: @11) {
	set(x) |- cup_i2(x, ?, ?)[
		self_in_singleton(x)[@h1]
	][as in(x, successor(x))]
}

"순서수(ordinal number)로서의 0."
$0_\omega$
cls ord_zero = emptyset;

"순서수(ordinal number)로서의 1. 0의 따름 순서수이다."
$1_\omega$
cls ord_one = successor(ord_zero);

"순서수(ordinal number)로서의 2. 1의 따름 순서수이다."
$2_\omega$
cls ord_two = successor(ord_one);

"따름 순서수는 집합이다."
theorem successor_is_set(cls x: @11) {
	set(x) |- cup_is_set[
		@h1;
		singleton_set_i[@h1]
	][as set(successor(x))]
}

"[$x]가 successor set이다."
$\left(<<\operatorname{successor\ set}>> #1\right)$
st is_successor_set(cls x) {
	A(
		in(emptyset, x),
		U((cls z) => {
			I(
				in(z ,x),
				in(successor(z), x)
			)
		})
	)
}

"무한 공리(axiom of infinity). 즉 successor set 중에는 집합인 것이 있다."
axiom ax_infinity() {
	X((cls x) => {
		A(
			set(x),
			is_successor_set(x)
		)
	})
}

"자연수 집합. 즉 모든 successor set을 교집합한 것이다."
$\omega$
cls omega = bigcap(setbuilder((cls z) => {
	is_successor_set(z)
}));

"자연수 집합은 0을 포함한다. `[Pinter]`에 따르면 페아노 공리계 제1공리이다."
theorem zero_in_omega() {
	bigcap_i(setbuilder((cls z) => {
		is_successor_set(z)
	}), ord_zero)[
		ax_emptyset();
		Ui[(cls z) => {
			cp[
				in(z, setbuilder((cls z) => is_successor_set(z))) |-
					Ae1[setbuilder_e[@h1]]
			]
		}]
	][as in(ord_zero, omega)]
}

"`[Pinter]`에 따르면 페아노 공리계 제2공리인 것."
theorem successor_in_omega(cls z: @11) {
	in(z, omega) |-
		bigcap_i(setbuilder((cls z) => is_successor_set(z)), successor(z))[
			successor_is_set(z)[Xi(contains(z), omega)[@h1]];
			Ui[(cls x) => {
				cp[
					in(x, setbuilder((cls z) => is_successor_set(z))) |-
						mp[
							mp[
								@h2;
								Ue(?, x)[bigcap_e(setbuilder((cls z) => {
									is_successor_set(z)
								}), z)[@h1]]
							];
							Ue(?, z)[Ae2[setbuilder_e((cls z) => is_successor_set(z), x)[
								@h2
							]]]
						]
				]
			}]
		][as in(successor(z), omega)]
}

"[$\omega]는 successor set이다."
theorem omega_is_successor_set() {
	Ai[
		zero_in_omega();
		Ui[(cls z) => {
			cp[successor_in_omega(z)]
		}]
	][as is_successor_set(omega)]
}

"자연수 집합은 집합이다. ax_infinity로부터 유도된다."
theorem omega_is_set() {
	mpX_Xe_p(?, set(omega))[
		ax_infinity();
		Ui[(cls x) => {
			cp[
				A(set(x), is_successor_set(x)) |-
					subset_is_set[
						Ae1[@h1];
						bigcap_is_smaller[setbuilder_ia((cls z) => is_successor_set(z), ?)[
							@h1
						]]
					][as set(omega)]
			]
		}]
	]
}

"`[Pinter]` 정리 6.3."
theorem induce_set($X$ cls x: @11) {
	subseteq(x, omega), in(ord_zero, x), Uin(omega, (cls n) => {
		I(in(n, x), in(successor(n), x))
	}) |-
		$x_is_successor_set = Ai[@h2; Ui[(cls n) => cp[in(n, x) |-
			mp[@h4; mp[in_subseteq[@h4; @h1]; Ue(?, n)[@h3]]]]]]
		[as is_successor_set(x)];
		
		$omega_subseteq_x =
			bigcap_is_smaller[setbuilder_i((cls z) => is_successor_set(z), ?)[
				subset_is_set[omega_is_set(); @h1];
				$x_is_successor_set
			]]
			[as subseteq(omega, x)];
		
		subseteq_to_eq[@h1; $omega_subseteq_x]
}

"수학적 귀납법. [~(href=https://www.cs.ru.nl/~freek/100/)[*Formalizing 100 Theorems]]의 정리 #74이다."
theorem induce(pr f) {
	$foo = (cls z) => {
		in(z, omega) |-
			Ei_alt[
				f(z) |- subsetbuilder_i(omega, f, z)[@h1; @h2];
				in(z, subsetbuilder(omega, f)) |- subsetbuilder_e[@h2]
			]
	};
	
	f(ord_zero), Uin(omega, (cls n) => {
		I(f(n), f(successor(n)))
	}) |-
		$c1 = subsetbuilder_is_subset(omega, f);
		$c2 = mpE1[@h1; $foo(ord_zero)[zero_in_omega()]];
		$c3 = Ui[(cls n) => cp[in(n, omega) |-
			cp[in(n, subsetbuilder(omega, f)) |- mpE1[
				mp[subsetbuilder_e[@h4]; mp[@h3; Ue(?, n)[@h2]]];
				$foo(successor(n))[successor_in_omega[@h3]]
			]]
		]]
		[as Uin(omega, (cls n) => {
			I(
				in(n, subsetbuilder(omega, f)),
				in(successor(n), subsetbuilder(omega, f))
			)
		})];
		
		Ui[(cls n) => cp[in(n, omega) |-
			subsetbuilder_e[in_eq_right_2[@h3; induce_set[$c1; $c2; $c3]]]
		]][as Uin(omega, f)]
}

"induce_set의 더 강력한 버전."
theorem induce_set_strong($X$ cls x: @12) {
	in(ord_zero, x), Uin(omega, (cls n) => {
		I(in(n, x), in(successor(n), x))
	}) |- induce(is_in(x))[@h1; @h2][as subseteq(omega, x)]
}

"`[Pinter]`에 따르면 페아노 공리계 제3공리인 것."
theorem successor_is_not_zero(cls n: @11) {
	in(n, omega) |-
		Ni_alt[
			eq(successor(n), ord_zero) |-
				Fi[
					in_eq_right_1[
						self_in_successor[set_i[@h1]];
						@h2
					];
					id(Nin(n, ord_zero))[emptyset_def(n)]
				]
		]
		[as Neq(successor(n), ord_zero)]
}

"`[Pinter]` 보조정리 6.6."
theorem omega_in_to_subseteq(cls n: @11, cls m: @21) {
	$base = Ui[(cls m) => cp[in(m, ord_zero) |-
		NNe[Ni_alt[N(subseteq(m, ord_zero)) |-
			Fi[@h1; emptyset_def(m)]
		]]
	]];
			
	$step = Ui[(cls n) => cp[in(n, omega) |-
		cp[U((cls m) => I(in(m, n), subseteq(m, n))) |-
			Ui[(cls m) => cp[in(m, successor(n)) |-
				$n_subseteq_successor_n = id(subseteq(n, successor(n)))[cup_is_bigger_1(n, singleton(n))];
				
				Oe_alt[
					cup_e[@h3];
					in(m, n) |-
						subseteq_subseteq[
							mp[@h4; Ue(?, m)[@h2]];
							$n_subseteq_successor_n
						];
					in(m, singleton(n)) |-
						eq_subseteq[
							singleton_e[set_i[@h1]; @h4];
							$n_subseteq_successor_n
						]
				]
			]]
		]
	]];
	
	in(n, omega), in(m, n) |-
		mp[@h2; Ue(?, m)[mp[@h1; Ue(?, n)[
			induce((cls n) => {
				U((cls m) => {
					I(in(m, n), subseteq(m, n))
				})
			})[$base; $step]
		]]]]
}

"`[Pinter]`에 따르면 페아노 공리계 제5공리인 것."
theorem successor_eq_e(cls n: @11, cls m: @21) {
	in(n, omega), in(m, omega), eq(successor(n), successor(m)) |-
		$base = setbuilder_e[in_eq_right_1[self_in_successor[set_i[@h1]]; @h3]];
		$base_dual = setbuilder_e[in_eq_right_2[self_in_successor[set_i[@h2]]; @h3]];
		
		$c1 = in(n, singleton(m)) |- singleton_e[set_i[@h2]; @h4];
		$c1_dual = in(m, singleton(n)) |- singleton_e[set_i[@h1]; @h4];
		
		NNe[Ni_alt[Neq(n, m) |-
			$c2 = omega_in_to_subseteq[@h2; Oe2[$base; Ni_alt[in(n, singleton(m)) |- Fi[$c1[@h5]; @h4]]]];
			$c2_dual = omega_in_to_subseteq[@h1; Oe2[$base_dual; Ni_alt[in(m, singleton(n)) |- Fi[$c1_dual[@h5]; Neq_symmetric[@h4]]]]];
			Fi[subseteq_to_eq[$c2; $c2_dual]; @h4]]]
}

st is_recursively_defined($\gamma$ cls g, $A$ cls a, cls c, cls f) {
	A(
		is_fun(g, omega, a),
		A(
			eq(funcall(g, ord_zero), c),
			Uin(omega, (cls n) => {
				eq(
					funcall(g, successor(n)),
					funcall(f, funcall(g, n))
				)
			})
		)
	)
}

st is_recursively_defined_m($\gamma$ cls g, $A$ cls a, cls c, fun f) {
	A(
		is_fun(g, omega, a),
		A(
			eq(funcall(g, ord_zero), c),
			Uin(omega, (cls n) => {
				eq(
					funcall(g, successor(n)),
					f(funcall(g, n))
				)
			})
		)
	)
}

"재귀정리의 증명에 사용되는 술어."
st is_recursive_supset($G$ cls g, $A$ cls a, cls c, cls f) {
	A(
		subseteq(g, cartesian(omega, a)),
		A(
			in(pair(ord_zero, c), g),
			Uin(omega, (cls n) => {
				Uin(a, (cls x) => {
					I(
						in(pair(n, x), g),
						in(pair(successor(n), funcall(f, x)), g)
					)
				})
			})
		)
	)
}

"클래스 [$A] 및 그 원소 [$c\in A] 및 함수 [$f: A\to A]에 대하여,
[ol
	[*] [$\gamma(0) = c,]
	[*] 임의의 [$n\in\omega]에 대하여, [$\gamma(n^+)=f(\gamma(n))]
]
을 만족하는 함수 [$\gamma:\omega\to A]를 만든다. [$c]와 [$f]가 조건을 만족하지 않으면 만들어지지 않는다. `[Pinter]`의 정리 6.8에 등장한다."
sealed cls define_recursively($A$ cls a, cls c, cls f) {
	bigcap3(($G$ cls g) => is_recursive_supset(g, a, c, f), ($G$ cls g) => g)
}

"메타함수 [$f]를 위한 define_recursively."
cls define_recursively_m($A$ cls a, cls c, fun f) {
	define_recursively(a, c, make_fun(f, a))
}

"is_recursive_supset의 첫 번째 조건. 즉 [$\gamma\subseteq\omega\times A]이다.
[$\gamma]가 함수가 되기 위한 첫 번째 조건이기도 하다."
theorem recursion_theorem_1($A$ cls a: @12, cls c: @11, cls f: @21) using define_recursively {
	in(c, a), is_fun(f, a, a) |-
		$irs = Ai[
			self_subseteq_self(cartesian(omega, a));
			Ai[
				cartesian_in_i[
					zero_in_omega();
					@h1
				];
				Ui((cls n) => {
					I(in(n, omega), Uin(a, (cls x) => {
						I(
							in(pair(n, x), cartesian(omega, a)),
							in(pair(successor(n), funcall(f, x)), cartesian(omega, a))
						)
					}))
				})[(cls n) => {
					cp[
						in(n, omega) |-
							Ui((cls x) => {
								I(in(x, a), I(
									in(pair(n, x), cartesian(omega, a)),
									in(pair(successor(n), funcall(f, x)), cartesian(omega, a))
								))
							})[(cls x) => {
								cp[
									in(x, a) |-
										cp[in(pair(n, x), cartesian(omega, a)) |-
											cartesian_in_i[
												successor_in_omega[@h3];
												funcall_in_codomain[@h2; @h4]
											]]
								]
							}]
							[as Uin(a, (cls x) => {
								I(
									in(pair(n, x), cartesian(omega, a)),
									in(pair(successor(n), funcall(f, x)), cartesian(omega, a))
								)
							})]
					]
				}]
				[as Uin(omega, (cls n) => {
					Uin(a, (cls x) => {
						I(
							in(pair(n, x), cartesian(omega, a)),
							in(pair(successor(n), funcall(f, x)), cartesian(omega, a))
						)
					})
				})]
			]
		][as is_recursive_supset(cartesian(omega, a), a, c, f)];
		
		bigcap3_is_smaller(?, ?, cartesian(omega, a))[$irs]
		[as subseteq(define_recursively(a, c, f), cartesian(omega, a))]
}

"is_recursive_supset의 두 번째 조건."
theorem recursion_theorem_2($A$ cls a: @12, cls c: @11, cls f) using define_recursively {
	in(c, a) |-
		$set_0c = pair_set_i[
			set_i[zero_in_omega()];
			set_i[@h1]
		];
		
		bigcap3_i[
			$set_0c;
			Ui[($G$ cls g) => cp[is_recursive_supset(g, a, c, f) |-
				Ae1[Ae2[@h2]]
			]]
		][as in(pair(ord_zero, c), define_recursively(a, c, f))]
}

"is_recursive_supset의 세 번째 조건."
theorem recursion_theorem_3($A$ cls a: @12, cls c, cls f: @11) using define_recursively {
	is_fun(f, a, a) |-
		with ($\gamma$ cls g = define_recursively(a, c, f)) {
			Ui[(cls n) => cp[in(n, omega) |-
				Ui[(cls x) => cp[in(x, a) |-
					cp[in(pair(n, x), g) |-
						bigcap3_i[
							pair_set_i[
								successor_is_set[set_i[@h2]];
								set_i[funcall_in_codomain[@h1; @h3]]
							];
							mp[
								setbuilder_e[@h4];
								UIm(
									($G$ cls g) => {
										I(is_recursive_supset(g, a, c, f), in(pair(n, x), g))
									},
									($G$ cls g) => {
										I(is_recursive_supset(g, a, c, f), in(pair(successor(n), funcall(f, x)), g))
									}
								)[Ui[($G$ cls g) => {
									I_distribute_left[cp[is_recursive_supset(g, a, c, f) |-
										mp[@h3; Ue(?, x)[mp[
											@h2;
											Ue(?, n)[Ae2[Ae2[@h5]]]
										]]]
									]]
								}]]
							]
						][as in(pair(successor(n), funcall(f, x)), define_recursively(a, c, f))]
					]
				]]
			]][as Uin(omega, (cls n) => {
				Uin(a, (cls x) => {
					I(
						in(pair(n, x), g),
						in(pair(successor(n), funcall(f, x)), g)
					)
				})
			})]
		}
}

"[$\gamma]가 함수가 되기 위한 두 번째 조건."
theorem recursion_theorem_4($A$ cls a: @12, cls c: @11, cls f: @21) {
	in(c, a), is_fun(f, a, a) |-
		with ($\gamma$ cls g = define_recursively(a, c, f)) {
			$base =
				Xi((cls x) => {
					A(in(x, a), in(pair(ord_zero, x), g))
				}, c)[Ai[
					@h1;
					recursion_theorem_2(a, c, f)[@h1]
					[as in(pair(ord_zero, c), g)]
				]]
				[as Xin(a, (cls x) => in(pair(ord_zero, x), g))];
			
			$step = Ui[(cls n) => cp[in(n, omega) |-
				cp[Xin(a, (cls x) => in(pair(n, x), g)) |-
					mpXin_Xe_p(?, ?, Xin(a, (cls x) => in(pair(successor(n), x), g)))[
						@h4;
						Ui[(cls x) => cp[in(x, a) |-
							cp[in(pair(n, x), g) |-
								Xi((cls x) => {
									A(in(x, a), in(pair(successor(n), x), g))
								}, funcall(f, x))[Ai[
									funcall_in_codomain[@h2; @h5];
									mp[@h6; mp[@h5; Ue(?, x)[mp[@h3; Ue(?, n)[
										recursion_theorem_3(a, c, f)[@h2]
										[as Uin(omega, (cls n) => {
											Uin(a, (cls x) => {
												I(
													in(pair(n, x), g),
													in(pair(successor(n), funcall(f, x)), g)
												)
											})
										})]
									]]]]]
								]]]
						]]
					]]
			]];
			
			induce((cls n) => {
				Xin(a, (cls x) => {
					in(pair(n, x), g)
				})
			})[$base; $step]
		}
}

"[$\gamma]가 함수가 되기 위한 세 번째 조건의 base case."
theorem recursion_theorem_5_base($A$ cls a: @12, cls c: @11, cls f: @21) using define_recursively {
	in(c, a), is_fun(f, a, a) |-
		with ($\gamma$ cls g = define_recursively(a, c, f)) {
			Qim((cls x) => {
				in(pair(ord_zero, x), g)
			}, c)[Ui[(cls d) => cp[in(pair(ord_zero, d), g) |-
				NNe[Ni_alt[Neq(d, c) |-
					$c1 = subseteq_subseteq[
						setminus_is_smaller(g, singleton(pair(ord_zero, d)));
						recursion_theorem_1[@h1; @h2]
						[as subseteq(g, cartesian(omega, a))]
					];
					
					$c2 = setminus_i[
						recursion_theorem_2(a, c, f)[@h1]
						[as in(pair(ord_zero, c), g)];
						Ni_alt[in(pair(ord_zero, c), singleton(pair(ord_zero, d))) |-
							Fi[
								eq_symmetric[pair_eq_e2[
									ax_emptyset(); set_i[@h1];
									ax_emptyset(); pair_set_e2[set_i[@h3]];
									singleton_e[set_i[@h3]; @h5]]];
								@h4
							]]
					];
					
					$c3 = Ui[(cls n) => cp[in(n, omega) |-
						Ui[(cls x) => cp[in(x, a) |-
							cp[in(pair(n, x), setminus(g, singleton(pair(ord_zero, d)))) |-
								setminus_i[
									mp[
										in_subseteq[
											@h7;
											setminus_is_smaller(g, singleton(pair(ord_zero, d)))
										];
										mp[@h6; Ue(?, x)[mp[@h5; Ue(?, n)[
											recursion_theorem_3(a, c, f)[@h2]
											[as Uin(omega, (cls n) => {
												Uin(a, (cls x) => {
													I(
														in(pair(n, x), g),
														in(pair(successor(n), funcall(f, x)), g)
													)
												})
											})]
										]]]]
									];
									Ni_alt[in(pair(successor(n), funcall(f, x)), singleton(pair(ord_zero, d))) |-
										Fi[
											pair_eq_e1_v2[set_i[@h3]; singleton_e[set_i[@h3]; @h8]];
											successor_is_not_zero[@h5]
										]]
								]]
						]]
					]];
					
					$gamma_is_smaller = bigcap3_is_smaller(
						?, ?,
						setminus(define_recursively(a, c, f), singleton(pair(ord_zero, d)))
					)[
						Ai[$c1; Ai[$c2; $c3]]
						[as is_recursive_supset(setminus(define_recursively(a, c, f), singleton(pair(ord_zero, d))), a, c, f)]
					][as subseteq(
						define_recursively(a, c, f),
						setminus(define_recursively(a, c, f), singleton(pair(ord_zero, d)))
					)];
					
					Fi[
						self_in_singleton[set_i[@h3]];
						setminus_e2[in_subseteq[@h3; $gamma_is_smaller]]
					]
				]]
			]]]
		}
}

"[$\gamma]가 함수가 되기 위한 세 번째 조건의 step case."
theorem recursion_theorem_5_step($A$ cls a: @12, cls c: @11, cls f: @21) using define_recursively {
	in(c, a), is_fun(f, a, a) |-
		with ($\gamma$ cls g = define_recursively(a, c, f)) {
			Ui[(cls n) => cp[in(n, omega) |-
				cp[Q((cls x) => {
					in(pair(n, x), g)
				}) |-
					mpXin_Xe_p(?, ?, Q((cls x) => {
						in(pair(successor(n), x), g)
					}))[
						mp[@h3; Ue(?, n)[
							recursion_theorem_4[@h1; @h2]
							[as Uin(omega, (cls n) => {
								Xin(a, (cls x) => {
									in(pair(n, x), g)
								})
							})]
						]];
						Ui[(cls x) => cp[in(x, a) |-
							cp[in(pair(n, x), g) |-
								Qim((cls x) => {
									in(pair(successor(n), x), g)
								}, funcall(f, x))[Ui[(cls u) => cp[in(pair(successor(n), u), g) |-
									NNe[Ni_alt[Neq(u, funcall(f, x)) |-
										$c1 = subseteq_subseteq[
											setminus_is_smaller(g, singleton(pair(successor(n), u)));
											recursion_theorem_1[@h1; @h2]
											[as subseteq(g, cartesian(omega, a))]
										];

										$c2 = setminus_i[
											recursion_theorem_2(a, c, f)[@h1]
											[as in(pair(ord_zero, c), g)];
											Ni_alt[in(pair(ord_zero, c), singleton(pair(successor(n), u))) |-
												Fi[
													eq_symmetric[pair_eq_e1_v2[
														set_i[@h7];
														singleton_e[set_i[@h7]; @h9]
													]];
													successor_is_not_zero[@h3]
												]]
										];

										$c3 = Ui[(cls m) => cp[in(m, omega) |-
											Ui[(cls v) => cp[in(v, a) |-
												cp[in(pair(m, v), setminus(g, singleton(pair(successor(n), u)))) |-
													$bar = in_subseteq[
														@h11;
														setminus_is_smaller(g, singleton(pair(successor(n), u)))
													];
													$foo = mp[
														$bar;
														mp[@h10; Ue(?, v)[mp[@h9; Ue(?, m)[
															recursion_theorem_3(a, c, f)[@h2]
															[as Uin(omega, (cls n) => {
																Uin(a, (cls x) => {
																	I(
																		in(pair(n, x), g),
																		in(pair(successor(n), funcall(f, x)), g)
																	)
																})
															})]
														]]]]
													];

													$c3_c1 = Neq(successor(m), successor(n)) |-
														setminus_i[
															$foo;
															Ni_alt[in(pair(successor(m), funcall(f, v)), singleton(pair(successor(n), u))) |-
																Fi[
																	pair_eq_e1_v2[
																		set_i[@h7];
																		singleton_e[set_i[@h7]; @h13]
																	];
																	@h12
																]]
														];

													$c3_c2 = eq(successor(m), successor(n)) |-
														in_eq_left_2[
															setminus_i[
																mp[@h6; mp[@h5; Ue(?, x)[mp[@h3; Ue(?, n)[
																	recursion_theorem_3(a, c, f)[@h2]
																	[as Uin(omega, (cls n) => {
																		Uin(a, (cls x) => {
																			I(
																				in(pair(n, x), g),
																				in(pair(successor(n), funcall(f, x)), g)
																			)
																		})
																	})]
																]]]]];
																Ni_alt[in(pair(successor(n), funcall(f, x)), singleton(pair(successor(n), u))) |-
																	Fi[
																		eq_symmetric[pair_eq_e2_v1[set_i[@h13]; singleton_e[set_i[@h7]; @h13]]];
																		@h8
																	]]
															];
															pair_eq_i[
																@h12;
																funcall_eq_i(f, ?, ?)[
																	mp[
																		Ai[
																			in_eq_left_1[
																				$bar;
																				pair_eq_i[
																					successor_eq_e[@h9; @h3; @h12];
																					eq_reflexive(v)
																				]
																			];
																			@h6
																		];
																		Ue2(?, v, x)[@h4]
																	]
																]
															]
														];

													Oe_alt[
														lxm(eq(successor(m), successor(n)));
														$c3_c2; $c3_c1
													]
												]
											]]
										]];

										$gamma_is_smaller = bigcap3_is_smaller(
											?, ?,
											setminus(define_recursively(a, c, f), singleton(pair(successor(n), u)))
										)[
											Ai[$c1; Ai[$c2; $c3]]
											[as is_recursive_supset(setminus(define_recursively(a, c, f), singleton(pair(successor(n), u))), a, c, f)]
										][as subseteq(
											define_recursively(a, c, f),
											setminus(define_recursively(a, c, f), singleton(pair(successor(n), u)))
										)];

										Fi[
											self_in_singleton[set_i[@h7]];
											setminus_e2[in_subseteq[@h7; $gamma_is_smaller]]
										]
									]]
								]]]
							]
						]]
					]
				]
			]][as Uin(omega, (cls n) => {
				I(
					Q((cls x) => {
						in(pair(n, x), g)
					}),
					Q((cls x) => {
						in(pair(successor(n), x), g)
					})
				)
			})]
		}
}

"[$\gamma]가 함수가 되기 위한 세 번째 조건."
theorem recursion_theorem_5($A$ cls a: @12, cls c: @11, cls f: @21) {
	in(c, a), is_fun(f, a, a) |-
		induce((cls n) => {
			Q((cls x) => {
				in(pair(n, x), define_recursively(a, c, f))
			})
		})[recursion_theorem_5_base[@h1; @h2]; recursion_theorem_5_step[@h1; @h2]]
}

"[$\gamma:\omega\to A]가 함수이다."
theorem recursion_theorem_6($A$ cls a: @12, cls c: @11, cls f: @21) {
	in(c, a), is_fun(f, a, a) |-
		Ai[recursion_theorem_1[@h1; @h2]; UinA_i[recursion_theorem_4[@h1; @h2]; recursion_theorem_5[@h1; @h2]]]
		[as is_fun(define_recursively(a, c, f), omega, a)]
}

"[$\gamma]가 함수이고 재귀적으로 정의된다."
theorem recursion_theorem_7($A$ cls a: @12, cls c: @11, cls f: @21) {
	in(c, a), is_fun(f, a, a) |-
		with ($\gamma$ cls g = define_recursively(a, c, f)) {
			$c1 = recursion_theorem_6[@h1; @h2]
			[as is_fun(g, omega, a)];
			
			$c2 = eq_symmetric[funcall_i[$c1; recursion_theorem_2(a, c, f)[@h1][as in(pair(ord_zero, c), g)]]];
			
			$c3 = Ui[(cls n) => cp[in(n, omega) |-
				mpXin_Xe_p(?, ?, eq(
					funcall(g, successor(n)),
					funcall(f, funcall(g, n))
				))[
					mp[@h3; Ue(?, n)[
						recursion_theorem_4[@h1; @h2]
						[as Uin(omega, (cls n) => {
							Xin(a, (cls x) => {
								in(pair(n, x), g)
							})
						})]
					]];
					Ui[(cls x) => cp[in(x, a) |-
						cp[in(pair(n, x), g) |-
							eq_transitive[
								eq_symmetric[funcall_i[
									$c1;
									mp[
										@h5;
										mp[@h4; Ue(?, x)[mp[@h3; Ue(?, n)[
											recursion_theorem_3(a, c, f)[@h2]
											[as Uin(omega, (cls n) => {
												Uin(a, (cls x) => {
													I(
														in(pair(n, x), g),
														in(pair(successor(n), funcall(f, x)), g)
													)
												})
											})]
										]]]]
									]
								]];
								funcall_eq_i(f, ?, ?)[funcall_i[$c1; @h5]]
							]]
					]]
				]
			]];
			
			Ai[$c1; Ai[$c2; $c3]]
			[as is_recursively_defined(g, a, c, f)]
		}
}

theorem recursion_theorem_7m($A$ cls a: @12, cls c: @11, fun f: @r4) {
	in(c, a), Uin(a, ($\gamma$ cls g) => in(f(g), a)) |-
		with ($\gamma$ cls g = define_recursively_m(a, c, f)) {
			$make_fun_f_is_fun = make_fun_is_fun(f, a, a)[@h2];
			
			$ird = recursion_theorem_7[@h1; $make_fun_f_is_fun]
			[as is_recursively_defined(g, a, c, make_fun(f, a))];
			
			$is_fun = Ae1[$ird];
			
			$rec = Ae2[$ird];
			$base = Ae1[$rec];
			$step = Ae2[$rec];
			
			$newstep = Ui[(cls n) => cp[in(n, omega) |-
				eq_transitive[mp[@h3; Ue(?, n)[$step]]; make_fun_funcall(f, a, a, ?)[@h2; funcall_in_codomain[$is_fun; @h3]]]
			]];
			
			Ai[$is_fun; Ai[$base; $newstep]]
			[as is_recursively_defined_m(g, a, c, f)]
		}
}

"[$\gamma]는 여러 개 있지 않다."
theorem recursion_theorem_8($A$ cls a: @12, cls c: @11, cls f: @21) {
	in(c, a), is_fun(f, a, a) |-
		Ui2[($\gamma_1$ cls g1, $\gamma_2$ cls g2) => {
			cp2[is_recursively_defined(g1, a, c, f), is_recursively_defined(g2, a, c, f) |-
				$g1_is_fun = Ae1[@h3];
				$g2_is_fun = Ae1[@h4];

				$base = eq_transitive[Ae1[Ae2[@h3]]; eq_symmetric[Ae1[Ae2[@h4]]]];

				$step = Ui[(cls n) => cp[in(n, omega) |-
					cp[eq(funcall(g1, n), funcall(g2, n)) |-
						eq_transitive[
							eq_transitive[
								mp[@h5; Ue(?, n)[Ae2[Ae2[@h3]]]];
								funcall_eq_i(f, ?, ?)[@h6]
							];
							eq_symmetric[mp[@h5; Ue(?, n)[Ae2[Ae2[@h4]]]]]
						]]
				]];

				fun_eq_i[
					$g1_is_fun; $g2_is_fun;
					induce((cls n) => eq(funcall(g1, n), funcall(g2, n)))[$base; $step]
				]
			]
		}][as Q(($\gamma$ cls g) => {
			is_recursively_defined(g, a, c, f)
		})]
}

"재귀 정리(recursion theorem)."
theorem recursion_theorem($A$ cls a: @12, cls c: @11, cls f: @21) {
	in(c, a), is_fun(f, a, a) |-
		XQ_to_Xq[
			Xi(($\gamma$ cls g) => {
				is_recursively_defined(g, a, c, f)
			}, define_recursively(a, c, f))[
				recursion_theorem_7(a, c, f)[@h1; @h2]
			];
			recursion_theorem_8(a, c, f)[@h1; @h2]
		]
}

"자연수를 더하는 연산. 단항연산이다."
$\left(#1<<+_\omega>>\cdot\right)$
cls omega_add_unary(cls n) {
	define_recursively_m(
		omega,
		n,
		($\gamma$ cls g) => successor(g)
	)
}

theorem successor_is_fun() {
	make_fun_is_fun(($\gamma$ cls g) => successor(g), omega, omega)[
		Ui[($\gamma$ cls g) => cp[in(g, omega) |- successor_in_omega[@h1]]]
	]
}

theorem successor_funcall(cls n: @11) {
	in(n, omega) |-
		make_fun_funcall(
			($\gamma$ cls g) => successor(g),
			omega, omega, ?
		)[
			Ui[($\gamma$ cls g) => cp[in(g, omega) |- successor_in_omega[@h2]]];
			@h1
		]
}

theorem omega_add_unary_ird(cls n) {
	in(n, omega) |-
		$foo = Ui[($\gamma$ cls g) => cp[
			in(g, omega) |- successor_in_omega[@h2]
		]][as Uin(omega, ($\gamma$ cls g) => in(successor(g), omega))];
		
		recursion_theorem_7m(?, ?, ($\gamma$ cls g) => successor(g))[@h1; $foo]
		[as is_recursively_defined_m(omega_add_unary(n), omega, n, ($\gamma$ cls g) => successor(g))]
}

"자연수 덧셈."
$!<prec=300>#1<<+_\omega>>#2$
cls omega_add(cls n, cls m) {
	funcall(omega_add_unary(n), m)
}

theorem omega_add_in_omega(cls n: @11, cls m: @21) {
	in(n, omega), in(m, omega) |-
		funcall_in_codomain[
			Ae1[omega_add_unary_ird(n)[@h1]];
			@h2
		][as in(omega_add(n, m), omega)]
}

"`[Pinter]` 6.10의 1."
theorem omega_add_base(cls n: @11) {
	in(n, omega) |- Ae1[Ae2[omega_add_unary_ird(n)[@h1]]][as eq(omega_add(n, ord_zero), n)]
}

"`[Pinter]` 6.10의 2."
theorem omega_add_step(cls n: @11, cls m: @21) {
	in(n, omega), in(m, omega) |-
		mp[@h2; Ue(?, m)[Ae2[Ae2[omega_add_unary_ird(n)[@h1]]]]]
		[as eq(omega_add(n, successor(m)), successor(omega_add(n, m)))]
}

theorem omega_add_one(cls n: @11) {
	in(n, omega) |-
		eq_transitive[
			omega_add_step[
				@h1; zero_in_omega()
			];
			successor_eq_i[omega_add_base[@h1]]
		][as eq(omega_add(n, ord_one), successor(n))]
}

"[$1+1=2]이다."
theorem one_plus_one_is_two() {
	omega_add_one(ord_one)[
		successor_in_omega[zero_in_omega()]
		[as in(ord_one, omega)]
	][as eq(omega_add(ord_one, ord_one), ord_two)]
}

"자연수를 곱하는 연산. 단항연산이다.

[$n\times 0 = 0] 및 [$n\times m^+ = n + n\times m]으로 재귀적으로 정의된다."
$\left(#1<<\times_\omega>>\cdot\right)$
cls omega_mul_unary(cls n) {
	define_recursively_m(
		omega,
		ord_zero,
		($\gamma$ cls g) => {
			omega_add(n, g)
		}
	)
}

"자연수 곱셈."
$!<prec=299>#1<<\times_\omega>>#2$
cls omega_mul(cls n, cls m) {
	funcall(omega_mul_unary(n), m)
}

$\left(n<<\mapsto>> {#1}^n{\left(#2\right)}\right)$
cls fun_iterated_(cls f, cls x) {
	define_recursively_m(
		rel_dom(f),
		x,
		($\gamma$ cls g) => funcall(f, g)
	)
}

theorem fun_iterated_ird(cls f: @11, cls x: @21, $A$ cls a: @12) {
	is_fun(f, a, a), in(x, a) |-
		$dom_f_is_a = fun_dom[@h1];
		
		recursion_theorem_7m[
			in_eq_right_2[@h2; $dom_f_is_a];
			Ui[(cls z) => cp[in(z, rel_dom(f)) |-
				$z_in_a = in_eq_right_1[@h3; $dom_f_is_a];
				in_eq_right_2[funcall_in_codomain[@h1; $z_in_a]; $dom_f_is_a]
			]]
		][as is_recursively_defined_m(fun_iterated_(f, x), rel_dom(f), x, ($\gamma$ cls g) => funcall(f, g))]
}

${#1}^{#2}{\left(#3\right)}{}^{\scriptscriptstyle<<\dag\text{discouraged}>>}$
sealed cls fun_iterated_call(cls f, cls n, cls x) {
	funcall(fun_iterated_(f, x), n)
}

"반복합성함수(iterated function). 자기함수(endofunction) [$f]의 합성을 [$n]번 한 것이다. [$f]가 자기함수일 때만 제대로 정의된다."
${#1}^{#2}{}^{\scriptscriptstyle<<\dag>>}$
cls fun_iterated(cls f, cls n) {
	make_fun((cls x) => fun_iterated_call(f, n, x), rel_dom(f))
}

theorem fun_iterated_def(cls f: @11, $A$ cls a: @12, cls n: @21, cls x: @31) using fun_iterated_call {
	is_fun(f, a, a), in(n, omega), in(x, a) |-
		
		$fun_dom = fun_dom[@h1];
		
		$dom_to_a = Ui[(cls x) => cp[in(x, rel_dom(f)) |-
			$x_in_a = in_eq_right_1[@h4; $fun_dom];

			in_eq_right_1[
				funcall_in_codomain[Ae1[fun_iterated_ird[@h1; $x_in_a]]; @h2]
				[as in(fun_iterated_call(f, n, x), rel_dom(f))];
				fun_dom[@h1]
			]
		]];
		
		make_fun_funcall(?, ?, a, ?)[$dom_to_a; in_eq_right_2[@h3; $fun_dom]]
		[as eq(
			funcall(fun_iterated(f, n), x),
			fun_iterated_call(f, n, x)
		)]
}

theorem fun_iterated_base(cls f: @11, cls x: @21, $A$ cls a: @12) using fun_iterated_call {
	is_fun(f, a, a), in(x, a) |-
		eq_transitive[
			fun_iterated_def[@h1; zero_in_omega(); @h2];
			Ae1[Ae2[fun_iterated_ird(f, x, a)[@h1; @h2]]]
			[as eq(fun_iterated_call(f, ord_zero, x), x)]
		]
}

theorem fun_iterated_is_fun(cls f: @11, $A$ cls a: @12, cls n: @21) using fun_iterated_call {
	is_fun(f, a, a), in(n, omega) |-
		$fun_dom = fun_dom[@h1];
		
		$dom_to_a = make_fun_is_fun[Ui[(cls x) => cp[in(x, rel_dom(f)) |-
			$x_in_a = in_eq_right_1[@h3; $fun_dom];

			in_eq_right_1[
				funcall_in_codomain[Ae1[fun_iterated_ird[@h1; $x_in_a]]; @h2]
				[as in(fun_iterated_call(f, n, x), rel_dom(f))];
				fun_dom[@h1]
			]
		]]][as is_fun(fun_iterated(f, n), rel_dom(f), a)];
		
		ax_indiscernible_m1(
			($B$ cls b) => is_fun(fun_iterated(f, n), b, a),
			?, ?
		)[$fun_dom; $dom_to_a]
}

theorem fun_iterated_im_base(cls f: @11, $A$ cls a: @12, $B$ cls b: @21) {
	is_fun(f, a, a), subseteq(b, a) |-
		$f0_is_fun = fun_iterated_is_fun[
			@h1;
			zero_in_omega()
		];
		
		$foo = Ui[(cls y) => {
			$c1 = cp[in(y, rel_im_set(fun_iterated(f, ord_zero), b)) |-
				$xy_in_f0 = setbuilder_e[@h3];

				$U = Ui[(cls x) => cp2[in(x, b), in(pair(x, y), fun_iterated(f, ord_zero)) |-
					$x_in_a = in_subseteq[@h4; @h2];

					$y_is_x = eq_transitive[
						funcall_i[
							$f0_is_fun;
							@h5
						];
						fun_iterated_base[@h1; in_subseteq[@h4; @h2]]
					];

					in_eq_left_2[@h4; $y_is_x]
				]];

				mpX_Xe_p(?, in(y, b))[$xy_in_f0; $U]
			];

			$c2 = cp[in(y, b) |-
				$y_in_a = in_subseteq[@h3; @h2];
				$yy_in_f0 = in_eq_left_1[
					funcall_def[$f0_is_fun; $y_in_a];
					pair_eq_i[
						eq_reflexive(y);
						fun_iterated_base[@h1; $y_in_a]
					]
				];

				setbuilder_i[
					set_i[@h3];
					Xi((cls x) => A(in(x, b), in(pair(x, y), fun_iterated(f, ord_zero))), y)[Ai[@h3; $yy_in_f0]]
				][as in(y, rel_im_set(fun_iterated(f, ord_zero), b))]
			];

			Ei[$c1; $c2]
		}];
		
		eq_i(rel_im_set(fun_iterated(f, ord_zero), b), b)[$foo]
}

theorem schroeder_bernstein_1($A$ cls a: @12, $B$ cls b: @13, cls f: @11, cls g: @21) {
	is_injection(f, a, b), is_injection(g, b, a) |-
		with ($A^\ast$ cls aa = bigcup2(omega, (cls n) => {
			rel_im_set(
				fun_iterated(rel_composite(g, f), n),
				setminus(a, rel_im(g))
			)
		})) {
			with ($\mathfrak h$ fun hh = (cls x) => if(
				in(x, aa),
				funcall(f, x),
				funcall(rel_inverse(g), x)
			)) {
				with (cls h = make_fun(hh, a)) {
					$f_is_fun = Ae1[@h1];
					$g_is_fun = Ae1[@h2];

					$y_in_b = Ui[(cls x) => cp[in(x, a) |-
						$b = lxm(in(x, aa));
						
						$c1 = cp[in(x, aa) |-
							$y_is_fx = if_def_1(?, funcall(f, x), funcall(rel_inverse(g), x))[@h4];
							in_eq_left_2[
								funcall_in_codomain[$f_is_fun; @h3];
								$y_is_fx
							]
						];
						
						$c2 = cp[Nin(x, aa) |-
							$x_in_a_minus_aa = setminus_i[@h3; @h4];
							$y_is_g_inv_x = if_def_2(?, funcall(f, x), funcall(rel_inverse(g), x))[@h4];
							
							$g_inv_is_bijection = bijection_inverse_is_bijection[injection_to_bijection[@h2]];
							$g_inv_is_function = Ae1[$g_inv_is_bijection];
							
							$gf_is_fun = fun_composite_is_fun[$f_is_fun; $g_is_fun];
							$f0_is_fun = fun_iterated_is_fun[$gf_is_fun; zero_in_omega()];
							
							$0 = fun_iterated_im_base[
								$gf_is_fun;
								setminus_is_smaller(a, rel_im(g))
							];
							
							$x_in_im_g = NNe[Ni_alt[Nin(x, rel_im(g)) |-
								Fi[
									bigcup2_i[
										zero_in_omega();
										in_eq_right_2[
											setminus_i[@h3; @h5]; $0
										]
									][as in(x, aa)];
									@h4
								]
							]];
							
							in_eq_left_2[
								funcall_in_codomain[$g_inv_is_function; $x_in_im_g];
								$y_is_g_inv_x
							]
						];
						
						Oe[$b; $c1; $c2]
					]];
					
					make_fun_is_fun[$y_in_b]
					[as is_fun(h, a, b)]
				}
			}
		}
}

schema schroeder_bernstein_2($A$ cls a, $B$ cls b, cls f, cls g) {
	is_injection(f, a, b), is_injection(g, b, a) |-
		with ($A^\ast$ cls aa = bigcup2(omega, (cls n) => {
			rel_im_set(
				fun_iterated(rel_composite(g, f), n),
				setminus(a, rel_im(g))
			)
		})) {
			with (cls h = make_fun(
				(cls x) => if(
					in(x, aa),
					funcall(f, x),
					funcall(rel_inverse(g), x)
				),
				a
			)) {
				is_bijection(h, a, b)
			}
		}
}

"Schr[&ouml]der[&ndash]Bernstein 정리."
schema schroeder_bernstein($A$ cls a, $B$ cls b) {
	X((cls f) => is_injection(f, a, b)), X((cls g) => is_injection(g, b, a)) |-
		X((cls h) => is_bijection(h, a, b))
}
